plugins {
    id 'java'
    id 'jacoco'
    id 'war'
}

group 'com.epam.esm'
version '1.0'

repositories {
    mavenCentral()
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

task jacocoGenerateRootReport(type: JacocoReport, group: 'Test Reporting') {
    description = 'Generates aggregate report from all subprojects.'

    def includeFilter = ['**/build/classes/java/**/*.class']
    def excludeFilter = ['']
    def classTree = fileTree(dir: "${rootProject.projectDir}", includes: includeFilter,  excludes: excludeFilter)
    def sourceTree = fileTree(dir: "${rootProject.projectDir}", includes: ['**/src/main/java/**/*.*'])
    def executionTree = fileTree(dir: "${rootProject.projectDir}", includes: ['**/build/jacoco/*.exec'])

    classDirectories.from = files([classTree])
    sourceDirectories.from = files([sourceTree])
    executionData.from = files([executionTree])

    reports {
        xml.required = true
        xml.destination file("${rootProject.projectDir}/build/reports/jacoco/report.xml")
    }
}

dependencies {
    implementation project(":controller")
    implementation project(":database")
    implementation project(":service")
    implementation 'org.springframework:spring-web:5.3.13'
    implementation 'org.springframework:spring-context:5.3.13'
    implementation 'org.springframework:spring-webmvc:5.3.13'
    testImplementation 'ch.qos.logback:logback-classic:1.2.7'
    compileOnly 'javax.servlet:javax.servlet-api:4.0.1'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoGenerateRootReport
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: "war"
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    repositories {
        mavenCentral()
    }
    test {
        useJUnitPlatform()
    }
    dependencies {
        testImplementation(platform('org.junit:junit-bom:5.8.1'))
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
        testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.1'
        compileOnly 'org.projectlombok:lombok:1.18.22'
        annotationProcessor 'org.projectlombok:lombok:1.18.22'
        implementation 'org.slf4j:slf4j-api:1.7.32'
        implementation 'org.slf4j:slf4j-simple:1.7.32'
        testImplementation 'org.mockito:mockito-core:4.1.0'
        testImplementation 'org.mockito:mockito-junit-jupiter:4.1.0'
    }

}